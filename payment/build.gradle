apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

ext {
  ver = "1.0.5.1"
}

android {
  defaultConfig {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    minSdkVersion 14
    versionCode 1
    versionName ver
  }
  buildTypes {
    debug {
      debuggable true
    }
    release {
      minifyEnabled false
      consumerProguardFiles 'proguard-payment.pro'
    }
  }
  publishNonDefault false
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:+'
}

group = 'uz.paycom'
version = ver

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = 'general'
    name = 'android-sdk'
    userOrg = 'paycom'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/PaycomUZ/AndroidSDK.git'
    version {
      name = ver
      desc = 'Paycom android sdk'
      vcsTag = ver
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}